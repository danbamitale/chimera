apply plugin: 'java'
apply plugin: 'maven-publish'

group 'com.vmantek.chimera'
version '1.2.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

ext {
    jposVersion = '2.1.7'
    jposeeVersion = '2.2.8'
    jposDeployerVersion = '1.1.3'
    javassistVersion = '3.28.0-GA'
    springBootVersion = '2.7.3'
}

repositories {
    mavenCentral()
    maven { url 'https://jpos.org/maven' }
    maven { url 'https://download.oracle.com/maven' }
}

configurations.all {
    exclude module: 'slf4j-nop'
}

dependencies {
    implementation 'org.jpos:jpos:' + jposVersion
    implementation 'org.javassist:javassist:' + javassistVersion
    implementation group: 'com.google.guava', name: 'guava', version: '30.0-jre'
    implementation 'org.freemarker:freemarker:2.3.31'

    implementation 'org.jpos.ee:jposee-dbsupport:' + jposeeVersion
    implementation 'org.jpos.ee:jposee-txn:' + jposeeVersion

    implementation 'org.springframework.boot:spring-boot-starter:' + springBootVersion
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:' + springBootVersion
    compileOnly 'org.springframework.boot:spring-boot-starter-web:' + springBootVersion
    compileOnly 'org.springframework.boot:spring-boot-actuator:' + springBootVersion
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor:' + springBootVersion

    implementation 'javax.xml.bind:jaxb-api:2.2.11'
    implementation 'com.sun.xml.bind:jaxb-core:2.2.11'
    implementation 'com.sun.xml.bind:jaxb-impl:2.2.11'
    implementation 'javax.activation:activation:1.1.1'
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:deprecation"
        }
    }
}

publishing {
    repositories {
        maven {
            name 'nexus'
            //noinspection HttpUrlsUsage
            def releasesUrl = 'https://repo.vmantek.com/repository/maven-releases/'
            def snapshotsUrl = 'https://repo.vmantek.com/repository/maven-snapshots/'
            url = project.hasProperty('release') ? releasesUrl : snapshotsUrl
            credentials {
                username project.nexusUser
                password project.nexusPassword
            }
        }
    }

    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}
